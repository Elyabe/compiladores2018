options
{
  DEBUG_PARSER = false;
  DEBUG_TOKEN_MANAGER = false;
}

PARSER_BEGIN(Compilador)

import java.io.*;
import java.util.LinkedList;
public class Compilador
{
   // Tabela de símbolos do compilador
   static public Tabela tab;
    
   public static void main(String args[])  throws ParseException
   {
      Compilador compilador = null;
	  tab = new Tabela();

      try
      {
         compilador = new Compilador(new FileInputStream("exemplo16.spc"));
         Compilador.inicio();
		 System.out.println(tab);
         System.out.println("Execução finalizada com sucesso!\n");
      }
      catch(FileNotFoundException e)
      {
         System.out.println("Erro: arquivo nao encontrado\n");
      }
      catch(TokenMgrError e)
      {
         System.out.println("Erro lexico:\n" + e.getMessage());
      }
      catch(ParseException e)
      {
		System.out.println("Erro Sintático:\n" + e.getMessage());
      }
      
   }
}
PARSER_END(Compilador)
 
SKIP : { " " | "\t" | "\r" | "\n" }

//TOKENS DE COMANDOS 
TOKEN [IGNORE_CASE] :
{
  		<EXIBE: "exibe" >  |
  		<LEITURA: "le" > | 
		<SE: "se" > |
      	<FIMSE: "fim-se" > |
      	<ENQUANTO: "enquanto" > |
      	<FIMENQUANTO: "fim-enquanto" > 
}

//TOKENS DE TIPOS 
TOKEN [IGNORE_CASE] :
{
  		<NUMERO: "numero" >  |
  		<PALAVRA: "palavra" >  
}
//----------CARACTERES----------
TOKEN : {
		< #CHAR: ["a"-"z"] | ["A" - "Z"] > | 
   		< AP: "(" > |
    	< FP: ")" > |
		< PV: ";" > |
		< VIRGULA: "," > |
    	< STRING: "\"" (~["\""])* "\"" > |
   		< VAR: (< CHAR >)+ (["a" - "z", "A" - "Z" ,"0" - "9", "_"])* > 
}
//-----------NUMEROS-----------
TOKEN : {
	< #DIGIT: ["0"-"9"] > |  
    < NUM: (< DIGIT >)* (".")? (< DIGIT >)+ > 
}
//----------OPERADORES---------
TOKEN : {
 	< SOMA: "+" > |
	< SUB: "-" > |
	< MUL: "*" > |
    < DIV: "/" > |
    < OU: "OR" > |
    < ATRIB: "<-"  > |
    < IGUAL: "=" > |
   	< CONCAT: "&" > 
}
  
void inicio() : { }
{
	( comando() )* < EOF > 
}

void pre_exp() : { LinkedList<Item > listaExp = new LinkedList<Item>(); }
{
	expressao( listaExp ){ System.out.println(listaExp);}
}


void expressao( LinkedList<Item > listaExp ) : { }
{
	( termo( listaExp ) ( < OU > termo( listaExp ) )* )
}

void termo(  LinkedList<Item > listaExp ) : { Token t; }
{
	termo1( listaExp )( < IGUAL > termo1( listaExp ) )?
}

void termo1( LinkedList<Item > listaExp ) : { }
{
	termo2( listaExp )( < CONCAT > termo2( listaExp ) )*
}

void termo2( LinkedList<Item > listaExp ) : {Token t; Item item;}
{
	termo3( listaExp )
	( ( t = < SOMA > | t = < SUB > ) termo3( listaExp ) {
		item = new Item( t.image );
		listaExp.add( item );
	  })*
}

void termo3( LinkedList<Item > listaExp ) : { Item item; Token t;}
{
	termo4( listaExp )( ( t = < MUL > | t = < DIV > ) termo4( listaExp )
	{
		item = new Item( t.image );
		listaExp.add(item);
    }
	)*
}

void termo4( LinkedList<Item > listaExp ) : { Item item; Token t; }
{
	< AP > expressao( listaExp ) < FP > |
	t = < NUM >
	{
	  item = new Item( t.image, Tipo.NUMERO );
	  listaExp.add(item);
	}|
	< SOMA > t = < NUM >
	{
	  item = new Item( t.image, Tipo.NUMERO );
	  listaExp.add(item);
	}|
	< SUB > t = < NUM >
	{
	  item = new Item( t.image, Tipo.NUMERO );
	  listaExp.add(item);
	}|
	t = < VAR > { tab.declaracaoPrevia(t); } |
	< STRING >
}

void comando() : { }
{
	atribuicao() |
	declaracao() |
	se() |
	enquanto() |
	le() |
	exibe()
}

void atribuicao() : { Token t;}
{
	t = < VAR > { tab.declaracaoPrevia(t);} < ATRIB > pre_exp() < PV >
}

void declaracao() : {Token t; char c_tp; }
{
	( < NUMERO > { c_tp = 'N'; } | < PALAVRA > { c_tp = 'S'; } )
	t = < VAR > { tab.criarVariavel( t, c_tp ); }( < ATRIB > pre_exp() )?
	( < VIRGULA > t =  < VAR > ( < ATRIB > pre_exp() )? { tab.criarVariavel( t, c_tp ); } )* < PV >
}

void se() : { }
{
	< SE > < AP > pre_exp() < FP > ( comando() )* < FIMSE >
}

void enquanto() : {}
{ 
	< ENQUANTO > < AP > pre_exp() < FP > ( comando() )* < FIMENQUANTO >
}

void le() : { Token t; }
{
	< LEITURA > t = < VAR > { tab.declaracaoPrevia(t); } ( < VIRGULA > t = < VAR > { tab.declaracaoPrevia(t); } )* < PV >
}

void exibe() : {}
{
	< EXIBE > pre_exp() ( < VIRGULA > pre_exp() )* < PV >
}